buildscript {
    configurations.classpath {
        resolutionStrategy {
            //this jgit version is required to fix this issue:
            //https://github.com/diffplug/spotless/issues/587 with the spotless plugin
            force 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
        }
    }
}

plugins {
    id "java-library"
    id "java-test-fixtures"
    id "jacoco"
    id "maven-publish"
    id "signing"
    id "io.codearte.nexus-staging" version "0.30.0"
    id "com.diffplug.spotless" version "5.11.0"
    id "com.github.ben-manes.versions" version "0.38.0"
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.sonarqube" version "3.1.1"
    id "org.unbroken-dome.test-sets" version "3.0.1"
}

testSets {
    integrationTest
}

group "com.github.michaelruocco"

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "spring-filters"
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
    jcenter()
}

ext {
    slf4jVersion = "1.7.30"
    lombokVersion = "1.18.18"
    jacksonVersion = "2.11.3"

    junitVersion = "5.7.1"
    junitPioneerVersion = "1.0.0"
    springBootVersion = "2.4.4"
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compileOnly "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

    api "com.github.michaelruocco:json-masker:1.0.6"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "commons-io:commons-io:2.8.0"
    implementation "org.apache.commons:commons-collections4:4.4"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation "org.mockito:mockito-core:3.8.0"

    integrationTestImplementation "org.springframework:spring-test:5.3.5"
    integrationTestImplementation "org.springframework.boot:spring-boot-test:${springBootVersion}"
    integrationTestImplementation "com.github.michaelruocco:file-loader:6.0.1"
    integrationTestImplementation "org.awaitility:awaitility:4.0.3"

    testFixturesApi "uk.org.webcompere:system-stubs-core:1.2.0"
    testFixturesApi "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    testFixturesApi "com.github.michaelruocco:string-utils:0.1.4"
    testFixturesApi "com.github.michaelruocco:testing-clocks:0.1.4"
    testFixturesCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testFixturesCompileOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    testFixturesCompileOnly "com.jayway.jsonpath:json-path:2.5.0"
    testFixturesAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams true
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

project.tasks["sonarqube"].dependsOn jacocoTestReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_spring-filters"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/report.xml"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def gitUrl = "https://github.com/michaelruocco/spring-filters"
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "mruoc"
            name "Michael Ruocco"
            email "michael.ruocco@hotmail.com"
        }
    }

    scm {
        url gitUrl
    }
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = toRepoUrl(project.version)
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        lib(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Library containing commonly used filters for a spring boot application")
                root.appendNode("name", project.name)
                root.appendNode("url", gitUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

signing {
    def signingKey = System.getenv("OSSRH_PGP_SECRET_KEY")
    def signingPassword = System.getenv("OSSRH_PGP_SECRET_KEY_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.lib
}
signLibPublication.enabled = gradle.startParameter.taskNames.contains("publish")

def toRepoUrl(version) {
    if (version.endsWith('SNAPSHOT')) {
        return "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    return "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

nexusStaging {
    username = System.getenv("OSSRH_USERNAME")
    password = System.getenv("OSSRH_PASSWORD")
}

spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
