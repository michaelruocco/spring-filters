buildscript {
    configurations.classpath {
        resolutionStrategy {
            //this jgit version is required to fix this issue:
            //https://github.com/diffplug/spotless/issues/587 with the spotless plugin
            force 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
        }
    }
}

plugins {
    id "java-library"
    id "java-test-fixtures"
    id "jacoco"
    id "com.diffplug.spotless" version "5.7.0"
    id "com.github.ben-manes.versions" version "0.33.0"
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.sonarqube" version "3.0"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.5"
    id "com.jfrog.artifactory" version "4.18.0"
}

group "com.github.michaelruocco"

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "spring-filters"
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
}

ext {
    slf4jVersion = "1.7.30"
    lombokVersion = "1.18.16"
    jacksonVersion = "2.11.3"

    junitVersion = "5.7.0"
    junitPioneerVersion = "1.0.0"
    mockitoVersion = "3.5.13"
    springVersion = "2.3.4.RELEASE"
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compileOnly "org.springframework.boot:spring-boot-starter-web:${springVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "commons-io:commons-io:2.8.0"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "com.github.michaelruocco:json-masker:1.0.2"

    testImplementation "org.springframework.boot:spring-boot-starter-web:${springVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.18.0"
    testImplementation "org.mockito:mockito-core:3.6.0"

    testFixturesApi "com.github.stefanbirkner:system-lambda:1.1.1"
    testFixturesCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testFixturesCompileOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    testFixturesAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams true
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

project.tasks["sonarqube"].dependsOn jacocoTestReport
sonarqube {
    properties {
        property "sonar.login", System.getenv("SONARCLOUD_TOKEN")
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_spring-filters"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/report.xml"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def pomConfig = {
    licenses {
        license {
            name "BSD License"
            url "http://www.opensource.org/licenses/bsd-license.php"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "mruoc"
            name "Michael Ruocco"
            email "michael.ruocco@hotmail.com"
        }
    }

    scm {
        url "https://github.com/michaelruocco/spring-filters"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Library containing commonly used filters for a spring boot application")
                root.appendNode("name", "spring-filters")
                root.appendNode("url", "https://github.com/michaelruocco/spring-filters")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["mavenPublication"]
    publish = true

    pkg {
        repo = "maven"
        name = "spring-filters"
        userOrg = "michaelruocco"
        licenses = ["bsd"]
        vcsUrl = "https://github.com/michaelruocco/spring-filters.git"
        version {
            name = project.version
            desc = project.version
            released = new Date()
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
            }
        }
    }
}

artifactory {
    contextUrl = "http://oss.jfrog.org"
    publish {
        repository {
            repoKey = "oss-snapshot-local"
            username = System.getenv("BINTRAY_USER")
            password = System.getenv("BINTRAY_KEY")
        }
        defaults {
            publications("mavenPublication")
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = "jcenter"
    }
    clientConfig.info.setBuildNumber(System.getProperty("build.number"))
}

spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
